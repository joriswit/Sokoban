apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")

android {
    lintOptions {
        abortOnError false
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            sokoban {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "com.starsep.sokoban"
        minSdkVersion 10
        targetSdkVersion 24
        versionCode 9
        versionName "1.8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.sokoban
            }
            return true
        }
        debug {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.sokoban
            }
            return true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile('org.powermock:powermock-api-mockito:1.6.2') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
    testCompile('org.powermock:powermock-module-junit4:1.6.2') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }

    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.google.android.gms:play-services-games:9.4.0'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:support-annotations:24.2.1'
    compile 'com.google.code.gson:gson:2.7'
}
